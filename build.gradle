apply plugin: 'groovy'
apply plugin: 'project-report'

group = 'org.example'
version = '0.0.0'

repositories {
    jcenter()
}

configurations.all {
    exclude group: 'commons-logging'
    exclude module: 'junit-dep'
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile "org.springframework.data:spring-data-jpa:${springDataJpaVersion}"
    compile "org.springframework:spring-core:${springFrameworkVersion}"
    compile "org.springframework:spring-beans:${springFrameworkVersion}"
    compile "org.springframework:spring-orm:${springFrameworkVersion}"
    compile "org.springframework:spring-context:${springFrameworkVersion}"
    compile "org.springframework:spring-aop:${springFrameworkVersion}"
    compile "org.springframework:spring-tx:${springFrameworkVersion}"
    compile "org.hibernate:hibernate-entitymanager:${hibernateVersion}"

    runtime "ch.qos.logback:logback-classic:${logbackVersion}"

    testCompile "org.springframework:spring-test:${springFrameworkVersion}"
    testCompile( "org.spockframework:spock-spring:${spockVersion}" ) {
        exclude module: 'groovy-all'
        exclude module: 'junit-dep'
    }
}

defaultTasks << 'clean'
defaultTasks << 'projectReport'
defaultTasks << 'build'

compileGroovy.groovyOptions.optimizationOptions['indy'] = false
compileTestGroovy.groovyOptions.optimizationOptions['indy'] = false

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

compileGroovy.options*.compilerArgs = ['-Xlint:deprecation','-encoding', 'UTF-8']
compileTestGroovy.options*.compilerArgs = ['-Xlint:deprecation','-encoding', 'UTF-8']

test {
    scanForTestClasses = true
    maxParallelForks = Runtime.runtime.availableProcessors()
    testLogging {
        showStandardStreams = false
        exceptionFormat = 'full'
        events( 'started', 'passed', 'skipped', 'failed' )
    }
}
